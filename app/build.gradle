plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}
apply from: "$rootDir/dependencies.gradle"

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionTemp = 0

def secrets = loadProperties("secret.properties")

android {
    signingConfigs {
        common {
            storeFile file(secrets['STORE_FILE'])
            storePassword secrets['STORE_PASSWORD']
            keyAlias secrets['STORE_KEY_ALIAS']
            keyPassword secrets['STORE_KEY_PASSWORD']
        }
    }
    compileSdk 32

    defaultConfig {
        applicationId "com.development.hiltpractices"
        minSdkVersion 23
        targetSdk 32
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionTemp
        resConfigs "en", "vi"
        archivesBaseName = "${versionMajor}.${versionMinor}.${versionPatch}.(${versionTemp})"

        buildConfigField "String", "API_KEY", secrets['API_KEY']
        manifestPlaceholders = [apiKey: secrets['API_KEY']]

        signingConfig signingConfigs.common

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
//        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        dataBinding true
    }
    hilt {
        enableAggregatingTask = true
    }

    flavorDimensions "default"

    productFlavors {

        def loadEnv = { target, envPath ->
            def envProperties = loadProperties(envPath)

            target.buildConfigField("String", "APP_BASE_URL",
                    envProperties['APP_BASE_URL'])

            target.buildConfigField("String", "APP_CLIENT_ID",
                    envProperties['APP_CLIENT_ID'])
        }

        dev {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}.(${versionTemp})"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            resValue("string", "app_name", "DEV")
            buildConfigField("boolean", "SHAKE_LOG", "true")
            loadEnv(owner, "env/dev.properties")
        }

        uat {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}.(${versionTemp})"
            applicationIdSuffix ".uat"
            versionNameSuffix "-uat"
            resValue("string", "app_name", "UAT")
            buildConfigField("boolean", "SHAKE_LOG", "true")
            loadEnv(owner, "env/uat.properties")
        }

        staging {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}.(${versionTemp})"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            resValue("string", "app_name", "Staging")
            buildConfigField("boolean", "SHAKE_LOG", "true")
            loadEnv(owner, "env/staging.properties")
        }

        product {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
            resValue("string", "app_name", "APP")
            buildConfigField("boolean", "SHAKE_LOG", "false")
            loadEnv(owner, "env/product.properties")
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
/*            FirebasePerformance {
                instrumentationEnabled false
            }*/
        }

        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
//    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation "androidx.security:security-crypto:$securityCryptoVersion"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"

    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"

    implementation "androidx.work:work-runtime-ktx:$workVersion"
    implementation "androidx.hilt:hilt-work:$hiltWorkVersion"
    kapt "androidx.hilt:hilt-compiler:$hiltWorkVersion"

    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-paging:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$moshiConverterVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    implementation "androidx.startup:startup-runtime:$startupVersion"

    implementation "androidx.legacy:legacy-support-v4:$legacyVersion"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.appcompat:appcompat-resources:$appCompatVersion"

    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    // For debug
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

    // For test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

kapt {
    correctErrorTypes true
}

def loadProperties(filename) {
    def propertiesFile = rootProject.file(filename)
    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))

    return properties
}

@SuppressWarnings('unused')
def findPropertyByKey(key) {
    return project.findProperty(key)
}
